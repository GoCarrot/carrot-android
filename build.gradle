import com.android.builder.core.BuilderConstants

import java.util.regex.Pattern

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.getkeepsafe.dexcount' // Outputs to ${buildDir}/outputs/dexcount/${variant}

dexcount {
    format = "list"
    includeClasses = false
    includeClassCount = false
    includeFieldCount = true
    includeTotalMethodCount = false
    orderByMethodCount = false
    verbose = false
    maxTreeDepth = Integer.MAX_VALUE
    runOnEachPackage = true
    maxMethodCount = 1550 // This includes android.* etc dependencies, so I guess just do it this way for now
}

gradle.projectsEvaluated {
    android.libraryVariants.each { variant ->
        def variantName = variant.name.capitalize();
        def proguardTask = project.tasks.findByName(
                "transformClassesAndResourcesWithProguardFor${variantName}")
        if (proguardTask) {
            // Compute UUID
            def sentry_debug_meta_properties = "${project.rootDir.toPath()}/build/intermediates/assets" +
                    "/${variant.dirName}/sentry-debug-meta.properties";
            def sentryProguardTask = project.tasks.create(
                    name: "processSentryProguardFor${variantName}",
                    type: Exec) {
                workingDir project.rootDir
                commandLine "sentry-cli", "upload-proguard", "--write-properties",
                        sentry_debug_meta_properties,
                        variant.getMappingFile(),
                        "--no-upload"
            }

            // Add UUID into resource string
            def teak_uuids_xml = "${project.rootDir.toPath()}/src/main/res/values/teak_uuids.xml"
            sentryProguardTask.doLast {
                def props = new Properties()
                file(sentry_debug_meta_properties).withInputStream { props.load(it) }

                def uuid = props.getProperty("io.sentry.ProguardUuids")
                println "> adding ${uuid} to resources."

                String contents = file(teak_uuids_xml).getText('UTF-8').replaceAll(/<\!ENTITY teak_proguard_uuid ".*">/, "<!ENTITY teak_proguard_uuid \"${uuid}\">")
                file(teak_uuids_xml).write(contents, 'UTF-8')
            }

            // Add to task chain
            proguardTask.finalizedBy sentryProguardTask
        }
    }
}

repositories {
    mavenCentral()
    google()
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

tasks.whenTaskAdded { task ->
    if(task.name.contains("Test")) {
        task.enabled = false
    }
}

android.libraryVariants.all { variant ->
    // Javadoc task
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        exclude '**/BuildConfig.java', '**/R.java'
    }

    // Jar task
    // https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Jar.html
    // ${android.defaultConfig.versionName}
    def jarTask = task("jar${variant.name.capitalize()}", type: Jar) {
        description "Bundles compiled .class files into a JAR file for $variant.name."
        dependsOn variant.javaCompile
        from variant.javaCompile.destinationDir
        exclude '**/R.class', '**/R$*.class', '**/R.html', '**/R.*.html', 'com/**'
        if (variant.buildType.name.equals(BuilderConstants.DEBUG)) {
            archiveName "${archivesBaseName}-${variant.name}.jar"
        } else {
            archiveName "${archivesBaseName}-${variant.name}.jar"
        }
    }
    artifacts.add('archives', jarTask)

    // Zip jar + res/
    artifacts.add('archives', task("zip${variant.name.capitalize()}", type: Zip) {
        description "Bundles .jar file and res/**.xml into a ZIP file for $variant.name."
        dependsOn jarTask
        if (variant.buildType.name.equals(BuilderConstants.DEBUG)) {
            archiveName "${archivesBaseName}-${variant.name}.zip"
        } else {
            archiveName "${archivesBaseName}-${variant.name}.zip"
        }

        from(jarTask.archivePath) {
            into('teak')
            rename(Pattern.quote(jarTask.archivePath.name), "teak.jar")
        }

        from fileTree(dir: "src/main", include: 'res/**')
    })
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    // IMPORTANT: Changing versions?  Update `setup` as well!
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 2
        versionName getVersionName()
        consumerProguardFiles 'proguard.txt'
        archivesBaseName = 'teak'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles 'proguard.txt', 'library.pro'
        }
    }
}

dependencies {
    implementation 'com.android.support:support-core-utils:26.0.0'

    // Google Play files - Old versions intentional, don't update them.
    implementation 'com.google.android.gms:play-services-gcm:10.2.1'
    implementation 'com.google.android.gms:play-services-ads:10.2.1'

    // Amazon, Air, Unity compile-time-only dependencies
    compileOnly files('libs/amazon-device-messaging-1.0.1.jar')
    compileOnly files('libs/in-app-purchasing-2.0.76.jar')
    compileOnly files('libs/FlashRuntimeExtensions.jar')
    compileOnly files('libs/UnityClasses.jar')
}
